
> moya

+ List\T {
    + length = 0
    - capacity = 30
    - items = T[30]
    
    ------------------------------------------------------------------------------------------------
        
    - grow(increment=30) do {
        oldSize = capacity
        capacity += increment
        items = resize(items, T, oldSize, capacity)
    }
    
    - normalizeIndex(index) do {
        if index < 0 {
            = length + index
        } else {
            = index
        }
    }
    
    ------------------------------------------------------------------------------------------------

    + !this do {
        = length == 0
    }

    + (this[index:Int32]):T {
        items[normalIndex]
    } where {
        normalIndex = normalizeIndex(index)
    } throws if {
        normalIndex >= length -> OutOfBoundsError("List index %index out of range")
    }
    
    + this[index:Int32] = item:T do {
        if (index >= capacity) {
            grow(index)
            length = index + 1
        }
        
        items[index] = item
    }
    
    ------------------------------------------------------------------------------------------------
    
    + add(item:T) do {
        if (length == capacity) {
            grow()
        }
        
        items[length] = item
        length = length + 1
    }
}
